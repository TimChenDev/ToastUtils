//文件开头配置
apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26

    resourcePrefix "toast_"

    defaultConfig {
        minSdkVersion 3
        targetSdkVersion 26
        versionCode 80
        versionName "8.0"

    }
}

dependencies{
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

publish {
    userOrg = 'getactivity'//填写bintray用户名，注意大小写
    groupId = 'com.hjq'//定义的maven group id最终引用形式
    artifactId = 'toast'//maven的artifact id
    version = '8.0'//maven 上发布版本号
    description = 'This is a very functional Toast'//描述，自己定义
    website = "https://github.com/getActivity/ToastUtils"//项目在github中的地址
}

tasks.withType(Javadoc) {//防止编码问题
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.sourceFiles
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}